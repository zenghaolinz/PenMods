import QtQuick 2.12

import "../commons"
import "../components"
import "../i18n"

YDictTypeBase {
    id: id_dict_type_ch_ancientword
    title: YTranslateText.dtChAncientWord
    visible: dictSelectParaphrasesJson !== null
    property var dictSelectParaphrasesJson: null

    Item {
        width: parent.width
        height: id_word_detailParas_column.height

        Column {
            id: id_word_detailParas_column
            spacing: 10
            width: parent.width
            property int paraItemTotalIndex: 0
            property var modelModelData: dictSelectParaphrasesJson === null || (typeof dictSelectParaphrasesJson.detailParas == "undefined")
                                         ? null : dictSelectParaphrasesJson.detailParas[0]
            onModelModelDataChanged: {
                paraItemTotalIndex = 0
            }

            YDictPinyinListView {
                id: id_dict_ch_pinyin_list_view
                chCharacter: resultManager.currentQuery
                visible: chCharacter.length === 1 && (isFirstDict || chPinyinListData.length > 1)
                isFirstDict: id_dict_type_ch_ancientword.isFirstDict
                onCurrentPinyinChanged: {
                    dictSelectParaphrasesJson = getDictSelectPinyinParaphrasesJson(dictJson, currentPinyin, dictType)
                }
                Component.onCompleted: {
                    id_dict_ch_pinyin_list_view.initPinyinListData(dictJson, dictType)
                    dictSelectParaphrasesJson = id_dict_ch_pinyin_list_view.getDictSelectPinyinParaphrasesJson(dictJson, id_dict_ch_pinyin_list_view.currentSelectedPinyin, dictType)
                    if (isFirstDict) {
                        resultManager.phoneticSymbolJson = id_dict_ch_pinyin_list_view.currentSelectedPinyin
                        if (settingManager.isAutoPronounce) {
                            id_dict_ch_pinyin_list_view.autoPlayPinyin()
                        }
                    }
                }
            }

            Repeater {
                id: id_word_detailParas_paraItems_repeater
                model: (typeof id_word_detailParas_column.modelModelData.paraItems == "undefined")
                       ? null : id_word_detailParas_column.modelModelData.paraItems

                Item {
                    id: id_word_detailParas_paraItems_item
                    width: id_word_detailParas_column.width
                    height: id_word_detailParas_paraItems_column.height
                    visible: index < 2

                    property var modelModelData: model.modelData

                    YTextBase {
                        id: id_word_detailParas_paraItems_index
                        color: YColors.grayText
                        font.pixelSize: 16
                        font.weight: Font.Bold
                        width: 28
                        height: id_word_detailParas_paraItems_column.height
                        Component.onCompleted: {
                            id_word_detailParas_column.paraItemTotalIndex += 1
                            text = id_word_detailParas_column.paraItemTotalIndex
                        }
                    }

                    Column {
                        id: id_word_detailParas_paraItems_column
                        spacing: 4
                        anchors.left: id_word_detailParas_paraItems_index.right
                        anchors.right: parent.right

                        YText {
                            id: id_word_detailParas_paraItems_para
                            width: parent.width
                            height: contentHeight
                            font.family: qmlGlobal.fontFamilyZhCn
                            font.weight: Font.Medium
                            textFormat: YTextBase.RichText
                            wrapMode: YTextBase.Wrap
                            text: {
                                if (!id_word_detailParas_paraItems_para.visible) return ""
                                let qsText = ""
                                if (id_word_detailParas_paraItems_item.modelModelData !== null && typeof id_word_detailParas_paraItems_item.modelModelData.wordsAttri != "undefined")
                                    qsText += ('<span style="color: %1">&lt;').arg(YColors.grayText) + id_word_detailParas_paraItems_item.modelModelData.wordsAttri + '&gt;</span>'
                                qsText += id_word_detailParas_paraItems_item.modelModelData.para
                                return qsText
                            }
                            visible: id_word_detailParas_paraItems_item.modelModelData !== null
                                     && typeof id_word_detailParas_paraItems_item.modelModelData.para != "undefined"
                        } // Text id_word_detailParas_paraItems_para

                        Repeater {
                            id: id_word_detailParas_paraItems_examples_repeater
                            model: id_word_detailParas_paraItems_item.modelModelData === null || (typeof id_word_detailParas_paraItems_item.modelModelData.examples == "undefined")
                                   ? null : id_word_detailParas_paraItems_item.modelModelData.examples

                            YTextBase {
                                id: id_word_detailParas_paraItems_example
                                width: parent.width
                                height: contentHeight
                                font.family: qmlGlobal.fontFamilyZhCn
                                font.pixelSize: 16
                                font.weight: Font.Normal
                                wrapMode: YTextBase.Wrap
                                color: YColors.grayText
                                text: model.modelData
                            } // Text id_word_detailParas_paraItems_example

                        } // Repeater id_word_detailParas_paraItems_examples_repeater

                    } // Column id_word_trs_sents_content_column

                } // Item id_word_trs_sents_item

            } // Column id_word_detailParas_column
        }
    }
}

/*
    // 一
{
    "phones":[{"pronun":"哪","phone":"nǎ"},{"pronun":"捺","phone":"nà"},{"pronun":"挪","phone":"nuó"},{"pronun":"懦","phone":"nuò"},{"pronun":"哪","phone":"né"}],
    "paraphrases":[
        {
            "detailParas":[
                {
                    "paraItems":[
                        {
                            "para":"见“那吒”。"
                        },
                        {
                            "para":"见ｎǎ。"
                        }
                    ]
                }
            ],
            "phone":"né",
            "words":"那"
        },
        {
            "detailParas":[
                {
                    "paraItems":[
                        {
                            "wordsAttri":"助",
                            "examples":[
                                "《后汉书·韩康传》：“公是韩伯休～?乃不二价乎?”"
                            ],
                            "para":"表疑问语气。"
                        },
                        {
                            "para":"见ｎǎ。"
                        }
                    ]
                }
            ],
            "phone":"nuò",
            "words":"那"
        },
        {
            "detailParas":[
                {
                    "paraItems":[
                        {
                            "wordsAttri":"代",
                            "examples":[
                                "《孔雀东南飞》：“处分适兄意，～得自任专?”",
                                "《乐府诗集·折杨柳枝歌》：“阿婆不嫁女，～得儿孙抱?”【注】古无“哪”字，古文中用“哪”的问句都用“那”。"
                            ],
                            "para":"同“哪”。如何；怎么。"
                        }
                    ]
                }
            ],
            "phone":"nǎ",
            "words":"那"
        },
        {
            "detailParas":[
                {
                    "paraItems":[
                        {
                            "wordsAttri":"代",
                            "para":"指较远的人或事物。"
                        }
                    ]
                }
            ],
            "phone":"nà",
            "words":"那"
        },
        {
            "detailParas":[
                {
                    "paraItems":[
                        {
                            "wordsAttri":"形",
                            "examples":[
                                "《诗经·商颂·那》：“猗与～与!”(猗：叹美之辞。)"
                            ],
                            "para":"多。"
                        },
                        {
                            "wordsAttri":"动",
                            "examples":[
                                "《左传·宣公二年》：“牛则有皮，犀兕尚多，弃甲则～。”"
                            ],
                            "para":"“奈何”的合音。"
                        },
                        {
                            "para":"见ｎǎ。"
                        }
                    ]
                }
            ],
            "phone":"nuó",
            "words":"那"
        }
    ],
    "word":"那",
    "relatedWords": {"end":["兀那"],"first":["那吒"],"empty":false}
}
  */

